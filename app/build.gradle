apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

// load api key from properties file
def properties = new Properties()
file("$rootDir/api-keys.properties").withInputStream { properties.load(it) }

static def version() {
  return 'git rev-parse --short HEAD'.execute().text.trim()
}

static def revision() {
  return 'git rev-list --count HEAD'.execute().text.trim()
}

android {
  compileSdkVersion 26
  buildToolsVersion '26.0.2'
  defaultConfig {
    applicationId "io.sikorka.android"
    minSdkVersion 17
    targetSdkVersion 26
    versionCode 1
    versionName "1.0.0-alpha.1"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true

    resValue "string", "maps_key", properties["google_maps_key"]

    buildConfigField "String", "GIT_HASH", "\"${version()}\""
    buildConfigField "String", "REVISION", "\"${revision()}\""


  }

  signingConfigs {
    debug {
      storeFile file("$rootDir/debug.keystore")
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  android.applicationVariants.all { variant ->
    variant.outputs.all {
      outputFileName = "${defaultConfig.applicationId}-${variant.versionCode}-v${variant.versionName}.apk"
    }
  }

}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'com.android.support:support-v4:27.0.0'
  implementation 'com.android.support:support-v13:27.0.0'
  implementation 'com.android.support:support-vector-drawable:27.0.0'
  androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })

  // Support libraries
  implementation 'com.android.support:appcompat-v7:27.0.0'
  implementation 'com.android.support:recyclerview-v7:27.0.0'
  implementation 'com.android.support:design:27.0.0'
  implementation 'com.android.support:preference-v7:27.0.0'
  implementation 'com.android.support:preference-v14:27.0.0'

  implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'

  // Custom build of master with an addition to an unmarshal issue
  // https://github.com/ethereum/go-ethereum/issues/14832#issuecomment-322965627
  implementation 'geth-android:geth:1.7.2-unstable'
  implementation 'com.squareup.okio:okio:1.13.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  //reactive extensions
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
// Because RxAndroid releases are few and far between, it is recommended you also
// explicitly depend on RxJava's latest version for bug fixes and new features.
  implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

  implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

  // Apache commons IO
  implementation group: 'commons-io', name: 'commons-io', version: '2.5'

  //dependency injection
  implementation 'com.github.stephanenicolas.toothpick:toothpick-runtime:1.1.1'
  implementation 'com.github.stephanenicolas.toothpick:smoothie:1.1.1'
  compileOnly 'javax.annotation:jsr250-api:1.0'

  kapt 'com.github.stephanenicolas.toothpick:toothpick-compiler:1.1.1'

  //android persistance (SQLite)

  // Database
  def roomVersion = "1.0.0"
  implementation "android.arch.persistence.room:runtime:$roomVersion"
  kapt "android.arch.persistence.room:compiler:$roomVersion"
  implementation "android.arch.persistence.room:rxjava2:$roomVersion"

  implementation "android.arch.lifecycle:extensions:1.0.0"

  //view injection
  implementation 'com.jakewharton:butterknife:8.8.1'
  kapt 'com.jakewharton:butterknife-compiler:8.8.1'

  implementation 'com.afollestad.material-dialogs:core:0.9.5.0'
  implementation 'com.afollestad.material-dialogs:commons:0.9.5.0'

  implementation 'com.github.apl-devs:appintro:v4.2.2'

  //Logging
  implementation 'com.jakewharton.timber:timber:4.6.0'

  implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

  //Location
  implementation 'com.google.android.gms:play-services-gcm:11.6.0'
  implementation 'com.google.android.gms:play-services-location:11.6.0'
  implementation 'com.google.android.gms:play-services-maps:11.6.0'

  //QR support
  implementation 'me.dm7.barcodescanner:zxing:1.9.8'

  debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

  //Unit testing
  testImplementation 'junit:junit:4.12'
  testImplementation 'com.github.stephanenicolas.toothpick:toothpick-testing:1.1.1'
  testImplementation "com.google.truth:truth:0.36"
  testImplementation "android.arch.core:core-testing:1.0.0"
}


kapt {
  arguments {
    arg('toothpick_registry_package_name', 'io.sikorka.android.di')
  }
}